buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}
allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
}


apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'checkstyle'
preBuild.dependsOn('checkstyle')
assemble.dependsOn('lint')
check.dependsOn('checkstyle')

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    /**
     * Define you Project Name here
     */
    def appName = 'Digi Tech'

    /**
     * For Internal user only
     */
    def APP_NAME = '\"' + (System.getenv("APP_NAME") ?: appName) + '\"'

    defaultConfig {
        applicationId "com.digitech.cp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 100
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "APP_NAME", APP_NAME
        resValue "string", "app_name", APP_NAME
        resValue "string", "facebook_app_id", "[PROJECT_FB_ID]"
        resValue "string", "fb_login_protocol_scheme", "fb[PROJECT_FB_ID]"
        manifestPlaceholders = [fabric_key: "4b99d2d21c7b5c9888d402bb7e7c5c851dc499c1"]
    }

    flavorDimensions "default"

    buildTypes {

        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    productFlavors {
        dev {
            buildConfigField 'String', 'BASE_URL', '"http://35.178.52.239:3000/"'
            buildConfigField 'String', 'FIREBASE_URL', '"https://firebasedynamiclinks.googleapis.com/"'
            buildConfigField 'boolean', 'WATER_MARK', 'true'

        }
        qa {
            buildConfigField 'String', 'BASE_URL', '"YOUR TEST URL"'
            buildConfigField 'String', 'FIREBASE_URL', '"https://firebasedynamiclinks.googleapis.com/"'
            buildConfigField 'boolean', 'WATER_MARK', 'true'

        }
        live {
            buildConfigField 'String', 'BASE_URL', '"YOUR_LIVE_URL"'
            buildConfigField 'String', 'FIREBASE_URL', '"https://firebasedynamiclinks.googleapis.com/"'
            buildConfigField 'boolean', 'WATER_MARK', 'false'

        }
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = ("${appName}_${variant.productFlavors[0].name}_${buildType.name}_v" +
                        "${defaultConfig.versionCode}").toLowerCase().replace(" ", "_") + ".apk"
            }
        }
    }
}

ext {
    supportLibraryVersion = "27.1.1"
    playServicesVersion = "12.0.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    //unit testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.0'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation "org.robolectric:robolectric:3.6.1"
    testImplementation 'org.json:json:20140107'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.0RC2'
    testImplementation 'org.powermock:powermock-module-junit4-rule-agent:1.7.0'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.7.0'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.0'
    // testImplementation "org.powermock:powermock-mockito-release-full:1.6.1"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    //android support dependencies
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"

    //android FCM dependencies
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // map and location dependencies
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"
    implementation "com.google.android.gms:play-services-places:$playServicesVersion"

    //paper db
    implementation 'io.paperdb:paperdb:2.6'

    //Calligraphy (custom font lib)
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'

    //facebook
    implementation 'com.facebook.android:facebook-android-sdk:4.31.0'

    //multi picker
    implementation 'com.kbeanie:multipicker:1.1.31@aar'

    //for cropping
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.3.+'

    //For Permission Handling
    implementation 'pub.devrel:easypermissions:1.1.1'

    //For Picking Country code
    implementation 'com.github.joielechong:countrycodepicker:2.1.5'
    compile('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true;
    }
}
apply plugin: 'com.google.gms.google-services'
